plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.sonarqube' version '4.4.1.3373'
	id 'jacoco'
}

group = 'com.laser'
version = '0.0.1-SNAPSHOT'

jar.enabled = false

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://repository.aspose.com/repo"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// mail
	implementation 'org.springframework.boot:spring-boot-starter-mail:3.1.5'

	// thymeleaf
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	// quartz scheduler
	implementation 'org.springframework.boot:spring-boot-starter-quartz'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// JWT
	compileOnly 'io.jsonwebtoken:jjwt-api:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2', 'io.jsonwebtoken:jjwt-jackson:0.11.2'

	// queryDSL
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// micrometer for prometheus
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

	// p6spy
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

	// AWS - S3
	implementation 'software.amazon.awssdk:s3:2.21.23'

	// aspose-cad
	implementation 'com.aspose:aspose-cad:23.7'

	// imgscalr
	implementation 'org.imgscalr:imgscalr-lib:4.2'

	// apache PDFBox
	implementation 'org.apache.pdfbox:pdfbox:3.0.0'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// h2, security, lombok for test
	testImplementation 'com.h2database:h2'
	testImplementation 'org.springframework.security:spring-security-test'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// test-containers
	testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.17.2'

	// slack
	implementation 'com.slack.api:slack-api-client:1.37.0'
}

task copyPrivate(type: Copy) {
	copy {
		from './laser-ordermanage-server-submodule-config'
		include 'application-prod.yml', '*.sql'
		into 'src/main/resources'
	}
	copy {
		from './laser-ordermanage-server-submodule-config'
		include 'application-test.yml', '*.sql'
		into 'src/test/resources'
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

def QDomains = []
for (qPattern in '**/QA'..'**/QZ') { // qPattern = '*.QA', '*.QB', ... '*.QZ'
	QDomains.add(qPattern + '**')
}

def jacocoExcludePatterns = [
		// 측정 안하고 싶은 패턴
		"com/laser/ordermanage/OrderManageApplication.class",
		"**/common/**",
		"**/dto/**",
		"**/exception/**",
		"**/type/**",
		"**/test/**",
		"**/resources/**"
]

jacocoTestCoverageVerification {

	violationRules {
		rule {
			enabled = false

			element = 'CLASS'

			// 라인 커버리지를 최소 80% 만족
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}

			// 브랜치 커버리지를 최소 80% 만족
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}
		}
	}

	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it, excludes: jacocoExcludePatterns + QDomains) // Querydsl 관련 제거
				})
		)
	}
}

def jacocoDir = layout.buildDirectory.dir("reports/")

jacocoTestReport {
	dependsOn test	// 테스트가 수행되어야만 report를 생성할 수 있도록 설정
	reports {
		html.required.set(true)
		xml.required.set(true)
		csv.required.set(true)
		html.destination jacocoDir.get().file("jacoco/index.html").asFile
		xml.destination jacocoDir.get().file("jacoco/index.xml").asFile
		csv.destination jacocoDir.get().file("jacoco/index.csv").asFile
	}

	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it, excludes: jacocoExcludePatterns + QDomains) // Querydsl 관련 제거
				})
		)
	}
	finalizedBy jacocoTestCoverageVerification
}

sonar {
	properties {
		property "sonar.projectKey", "AppLinkers_LaserOrderManage-server"
		property "sonar.organization", "applinkers"
		property "sonar.host.url", "https://sonarcloud.io"
		property 'sonar.sources', 'src'
		property 'sonar.language', 'java'
		property 'sonar.sourceEncoding', 'UTF-8'
		property 'sonar.test.exclusions', jacocoExcludePatterns.join(',')
		property 'sonar.test.inclusions', '**/*Test.java'
		property 'sonar.java.coveragePlugin', 'jacoco'
		property 'sonar.coverage.jacoco.xmlReportPaths', jacocoDir.get().file("jacoco/index.xml").asFile
	}
}